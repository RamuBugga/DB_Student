IF OBJECT_ID('USP_GET_StudentProfile', 'P') IS NOT NULL
    DROP PROCEDURE USP_GET_StudentProfile;
GO

CREATE PROCEDURE USP_GET_StudentProfile
    @StudentID INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        -- Student
        S.StudentID,
        S.StudentFullName,
        S.FirstName,
        S.MiddleName,
        S.LastName,
        S.Gender,
        S.DOB,
        S.AdharNo,
        S.APARNo,
        S.StatusID,
        S.StudentSegmentID,
        SEG.SegmentName,

        -- Address
        A.StudentParentDetailID,
        A.HouseNo,
        A.StreetName,
        A.Locality,
        A.PinCode,
        A.CityID,
        CT.CityName,
        A.StateID,
        ST.StateName,
        A.CountryID,
        CO.CountryName,

        -- Class
        SC.StudentClassDetailID,
        SC.GradeID,
        CLS.ClassName,
        SC.ClassRoomID,
        CR.ClassRoomName,

        S.CreatedDate,

        -- Parent Details as JSON
        (
            SELECT 
                P.StudentParentDetailID,
                P.StudentID,
                P.ParentName,
                P.ParentRelation,
                P.MobileNo,
                P.EmailID
            FROM StudentParentDetails P
            WHERE P.StudentID = S.StudentID
            FOR JSON PATH
        ) AS ParentsJson

    FROM StudentDetails S
    LEFT JOIN StudentSegments SEG ON S.StudentSegmentID = SEG.StudentSegmentID

    LEFT JOIN StudentAddressDetails A ON S.StudentID = A.StudentID
    LEFT JOIN Countries CO ON A.CountryID = CO.CountryID
    LEFT JOIN States ST ON A.StateID = ST.StateID
    LEFT JOIN Cities CT ON A.CityID = CT.CityID

    LEFT JOIN StudentClassDetails SC ON S.StudentID = SC.StudentID
    LEFT JOIN Classes CLS ON SC.GradeID = CLS.ClassID
    LEFT JOIN ClassRooms CR ON SC.ClassRoomID = CR.ClassRoomID

    WHERE S.StudentID = @StudentID;
END
GO
